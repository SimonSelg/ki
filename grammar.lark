// A ``file`` is a sequence of 0 or more ``note`` rules.
file: note*

// A ``note`` is a group of exactly 1 ``header`` and at least 1 ``field``.
note: (header field+)
field: "## " FIELDNAME "\n" (FIELDLINE "\n")* "\n"*

// A ``header`` is sequence of metadata rules, all required.
header: (TITLE NID MODEL tags MARKDOWN) "\n"+
tags: ("tags: " [ESCAPED_STRING ("," ESCAPED_STRING)*]) "\n"

// Titles are alphameric + some limited punctuation.
TITLE: "# " /[ a-zA-Z0-9_-]/+ "\n"
NID: "nid: " INT "\n"
MODEL: "model: " MODELNAME "\n"
MARKDOWN: "markdown: " ("true" | "false") "\n"
MODELNAME: ANKINAME
FIELDNAME: ANKINAME
ANKINAME: /[^#^\/\n\s].{0,}/
FIELDLINE: /[^#\n].{0,}/

%import common.ESCAPED_STRING
%import common.INT
%import common.WS
// %ignore WS
